import org.gradle.api.artifacts.maven.MavenDeployment

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.6

group = 'com.txtr'
version = '1.0.1'
ext.packaging = 'jar'

repositories {
    mavenCentral()
}

configurations.compile {
    resolutionStrategy.cacheChangingModulesFor 0, "seconds"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task exportJavadoc(type: Copy, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    into 'javadoc'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

dependencies {
    compile "com.google.guava:guava:10.0"
    compile "com.google.code.findbugs:jsr305:1.3.7"

    // junit testing
    testCompile "junit:junit:4.1"
    testCompile "org.mockito:mockito-all:1.9.0"
}

// enable Gradle Wrapper support for bootstrapping with specific version
task wrapper(type: Wrapper) {
    gradleVersion = '1.0-rc-1'
}

//********* artifact signing *********
if(gradle.startParameter.taskNames.contains("uploadArchives")) {
    signing {
        sign configurations.archives
    }
} else {
    task signArchives {
        // do nothing
    }
}

uploadArchives {
    repositories {
        if (version.contains("SNAPSHOT")) {
            mavenLocal()
        } else {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "http://ci:8080/artifactory/ext-releases-local")

                pom.project {
                    name 'guava-retrying'
                    packaging 'jar'
                    description "This is a small extension to Google's Guava library to allow for the creation of configurable retrying strategies for an arbitrary function call, such as something that talks to a remote service with flaky uptime."
                    url 'http://txtr.github.com'

                    scm {
                        url 'scm:git@github.com:txtr/guava-retrying.git'
                        connection 'scm:git@github.com:txtr/guava-retrying.git'
                        developerConnection 'scm:git@github.com:txtr/guave-retrying.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'txtr'
                            name 'txtr'
                        }
                    }
                }
            }
        }
    }
}
